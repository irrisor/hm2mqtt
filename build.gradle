version = '0.19.1'

apply plugin: 'java'
apply plugin: 'eclipse'

sourceCompatibility = 1.8
targetCompatibility = 1.8

[ compileJava, compileTestJava ]*.options*.encoding = 'UTF-8'

repositories {
	mavenCentral()
	maven { 
		url 'https://repo.eclipse.org/content/repositories/paho-releases/'
	} 
}

dependencies {
	compile 'com.eclipsesource.minimal-json:minimal-json:0.9.4'
	compile 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.0.2'
	compile group: 'org.apache.xmlrpc', name: 'xmlrpc-client', version: '3.1.3'
	compile group: 'org.apache.xmlrpc', name: 'xmlrpc-server', version: '3.1.3'
}

task listJars {
	doLast {
		configurations.compile.each { File file -> println file.name }
	}
}

jar {
	from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } } { exclude "META-INF/**" }
	archiveName 'hm2mqtt.jar'
	manifest {
        attributes 	'Main-Class': 'com.tellerulam.hm2mqtt.Main',
        			'Implementation-Title': 'hm2mqtt Homematic to MQTT bridge',
                   	'Implementation-Version': version
    }
}

task addon(dependsOn: 'jar', type: Tar) {
	compression = Compression.GZIP
	archiveName 'hm2mqtt-addon.tar.gz'
	into('addon') {
		from 'hm2mqtt-addon/addon', 'build/libs/hm2mqtt.jar'
	}
	into('rc.d') {
		from 'hm2mqtt-addon/rc.d'
	}
	into('') {
		from 'hm2mqtt-addon/update_script', 'hm2mqtt-addon/hm2mqtt_addon.cfg', 'hm2mqtt-addon/VERSION'
	}
}

uploadArchives {
    repositories {
       flatDir {
           dirs 'build'
       }
    }
}
